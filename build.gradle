plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '3.2.1'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'io.github.devatherock'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'
targetCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    def lombokVersion = '1.18.22'
    def spockVersion = '2.1-groovy-3.0'

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    implementation group: 'org.postgresql', name: 'postgresql'
    implementation(platform(group: 'org.springframework.cloud', name: 'spring-cloud-dependencies', version: '2023.0.0'))
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-config-server'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap'

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testImplementation group: 'org.spockframework', name: 'spock-core', version: spockVersion
    testImplementation group: 'org.spockframework', name: 'spock-spring', version: spockVersion
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = Boolean.getBoolean('all.logs')
        events 'passed', 'failed'
    }
}

jar {
    enabled = false
}

bootRun {
    systemProperties(System.properties)
}
